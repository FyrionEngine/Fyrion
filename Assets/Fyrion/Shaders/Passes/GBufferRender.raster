struct VSInput
{
    float3 position  : POSITION0;
    float3 normal    : NORMAL1;
    float3 color     : COLOR2;
    float2 uv        : UV2;
    float4 tangent   : TANGENT1;
};

struct VSOutput
{
    float4 pos      : SV_POSITION;
    float3 fragPos  : POSITION0;
    float3 color    : COLOR0;
    float2 uv       : TEXCOORD0;
    float3 normal   : NORMAL1;
    float4 tangent  : TANGENT1;
};

struct PushConsts
{
    float4x4 model;
};

struct Scene
{
    float4x4 viewProjection;
};

ConstantBuffer<Scene> scene : register(b0);
[[vk::push_constant]]  ConstantBuffer<PushConsts>  pushConsts;

VSOutput MainVS(VSInput input)
{
    VSOutput output = (VSOutput)0;
    output.fragPos    = (float3)mul(pushConsts.model, float4(input.position, 1.0));
    output.color      = input.color;
    output.pos        = mul(scene.viewProjection, float4(output.fragPos, 1.0));
    output.uv         = input.uv;

    float3x3 normalMat = (float3x3)pushConsts.model;
    output.normal      = normalize(mul(normalMat, input.normal));
    output.tangent     = float4(normalize(mul(normalMat, input.tangent.xyz)), input.tangent.w);

    return output;
}

//pixel shader

struct Material
{
    float4 baseColorAlphaCutOff;
    float4 uvScaleNormalMultiplier;
    float4 metallicRoughness;
    float4 emissiveFactor;
    float4 textureProps; //hasNormal. hasMetallic, hasRoughness,  hasMetallicRoughness
};

struct PSOutput
{
    float4 outAlbedoMetallic    :  SV_TARGET0;
    float4 outNormalRoughness   :  SV_TARGET1;
    float4 outPositionAo        :  SV_TARGET2;
};

ConstantBuffer<Material> material       : register(b0, space1);

Texture2D       baseColorTexture            : register(t1, space1);
SamplerState    defaultSampler              : register(s2, space1);
Texture2D       normalTexture               : register(t3, space1);
Texture2D       roughnessTexture            : register(t4, space1);
Texture2D       metallicTexture             : register(t5, space1);
Texture2D       metallicRoughnessTexture    : register(t6, space1);


PSOutput MainPS(VSOutput input)
{
    float2 uv = input.uv;
    float3 baseColor    = material.baseColorAlphaCutOff.rgb;
    float  alphaCutOff   = material.baseColorAlphaCutOff.a;

    float4 color = baseColorTexture.Sample(defaultSampler, uv) * float4(baseColor, 1.0);

    if (color.a < alphaCutOff)
    {
        discard;
    }

    color = pow(color,2.2);

    float roughness     = material.metallicRoughness.x;
    float metallic      = material.metallicRoughness.y;

    float3 normal = input.normal;

    if (material.textureProps.x == 1.0)
    {
        float3 binormal = input.tangent.w * cross(input.normal, input.tangent.xyz);
        float3x3 TBN    = transpose(float3x3(input.tangent.xyz, binormal, input.normal));
        float3 n        = normalTexture.Sample(defaultSampler, uv).xyz * 2.0 - 1.0; //replace sampler

        normal   = mul(TBN, n);
    }

    if (material.textureProps.y == 1.0)
    {
        metallic = metallicTexture.Sample(defaultSampler, uv).r;
    }

    if (material.textureProps.z == 1.0)
    {
        roughness = roughnessTexture.Sample(defaultSampler, uv).r;
    }

    if (material.textureProps.w == 1.0)
    {
        float2 metallicRoughness = metallicRoughnessTexture.Sample(defaultSampler, uv).zy;
        metallic = metallicRoughness.x;
        roughness = metallicRoughness.y;
    }

    PSOutput output = (PSOutput)0;
    output.outAlbedoMetallic  = float4(color.rgb, metallic);
    output.outNormalRoughness = float4(normal, roughness);
    output.outPositionAo      = float4(input.fragPos, 1.0);

    return output;
}
//end pixel shader
